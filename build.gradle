// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    apply from: 'versions.gradle'

    repositories {
        google()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$versions.gradle"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "com.google.gms:google-services:$versions.google_services"
        classpath "com.google.firebase:firebase-crashlytics-gradle:$versions.firebase_crash_gradle"
    }
}

plugins {
    id 'com.diffplug.gradle.spotless' version '3.28.0'
    id "com.github.ben-manes.versions" version '0.39.0'
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(versions.ktlint)
            licenseHeaderFile project.rootProject.file('scripts/copyright.kt')
        }
        groovyGradle {
            target '**/*.gradle'
            greclipse().configFile(project.rootProject.file('scripts/greclipse.properties'))
            paddedCell() // to avoid cyclic ambiguities
        }
    }
}